/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2022-2024 The Jarviz authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath "gradle.plugin.com.github.johnrengelman:shadow:$shadowPluginVersion"
        classpath "org.kordamp.gradle:java-project-gradle-plugin:$kordampPluginVersion"
        classpath "org.kordamp.gradle:checkstyle-gradle-plugin:$kordampPluginVersion"
        classpath "org.kordamp.gradle:kordamp-parentbuild:$kordampBuildVersion"
        classpath "org.jreleaser:jdks-gradle-plugin:$jdksPluginVersion"
        classpath "org.moditect:moditect-gradle-plugin:$moditectPluginVersion"
        classpath "org.ow2.asm:asm:$asmVersion"
        classpath "org.ow2.asm:asm-commons:$asmVersion"
        classpath "org.ow2.asm:asm-analysis:$asmVersion"
        classpath "org.ow2.asm:asm-tree:$asmVersion"
    }
}

apply plugin: 'org.kordamp.gradle.kordamp-parentbuild'

rootProject.name = 'jarviz'

projects {
    directories = ['core', 'plugins']

    plugins {
        all {
            id 'idea'
        }
        path(':') {
            id 'org.kordamp.gradle.java-project'
            id 'org.kordamp.gradle.checkstyle'
        }
        dirs(['core', 'plugins']) {
            id 'java-library'
        }
    }
}

enforce {
    mergeStrategy = 'append'

    rule(enforcer.rules.BanDuplicateClasses) { r ->
        r.ignoreWhenIdentical.set(true)
        r.dependency("com.google.guava:guava:$guavaVersion".toString()) { d->
            d.ignore('com.google.common.*')
        }
    }

    rule(enforcer.rules.ForceDependencies) { r ->
        r.dependencies.addAll "org.codehaus.plexus:plexus-utils:$plexusVersion",
            "com.google.errorprone:error_prone_annotations:$errorproneVersion",
            "com.google.guava:guava:$guavaVersion",
            "commons-codec:commons-codec:$commonsCodecVersion",
            "org.apache.commons:commons-lang3:$commonsLang3Version",
            "org.apache.httpcomponents:httpcore:$httpcoreVersion",
            "org.codehaus.plexus:plexus-classworlds:$classworldsVersion",
            "org.codehaus.plexus:plexus-component-annotations:$plexusAnnotationsVersion"
    }
}